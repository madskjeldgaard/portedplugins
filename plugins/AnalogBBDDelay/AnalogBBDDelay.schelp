CLASS:: AnalogBBDDelay
SUMMARY:: A virtual analog model of a Bucket Brigade Delay chip
RELATED:: Classes/Delay
CATEGORIES::UGens>Delay

DESCRIPTION::

This UGen models the behaviour of a Bucket-brigade delay unit - an integrated circuit used to create delay effects in audio equipment from the 1960's to the 1980's.

BBD's are quite noisy in themselves so they are usually accompanied by band limiting filters and companders at the input and output of the circuitry. The filters used at the ins and outs of the model in this UGen are based on the Roland Juno 60.

This is a port of code originally written by J.P. Cimalando (LINK::https://jpcima.sdf1.org/::), specifically from "rc-playground": LINK::https://github.com/jpcima/rc-effect-playground::).

References:

Raffel, C., & Smith, J. (2010, September)
Practical modeling of bucket-brigade device circuits - LINK::http://colinraffel.com/publications/dafx2010practical.pdf::

CLASSMETHODS::

METHOD::ar

ARGUMENT::input
Sound input

ARGUMENT::delaytime
Note, this should be really small because BBD's typically don't have a lot of delay (a 1024 stage BBD delay chip has around 5 ms of delay time for example).

ARGUMENT::feedback
Amount of signal fed back from the output of the BBD to the input. Be careful with this one. Range is 0.0 to 1.0 approximately.

ARGUMENT::numStages
Number of stages in the virtual BBD delay chip. More stages result in more CPU usage. Typical values are 128, 256, 512, 1024 and 8192.

EXAMPLES::

code::
(
// A deep chorus wannabe not-really-actually-a Juno 60
Ndef(\pseudo60, {|modFreq=0.01|
	var delayed, sig;
	var clean = NeoVarSawOsc.ar(freq: 100 * [1,1.25,1.75],  pw: 0.5,  waveshape: 0.65).sum / 3;
	var numChorus=4;

	delayed = Array.fill(numChorus, {|i|
		AnalogBBDDelay.ar(
			clean,
			delaytime: SinOsc.kr(modFreq * (i+1), phase: [0, 0.5pi]).range(5e-3, 1e-3),
			feedback: LFNoise2.kr(0.1).range(0.0,0.25),
			numStages: 183
		)
	});

	delayed = delayed.sum;

	sig = XFade2.ar(inA: clean,  inB: delayed,  pan: 0.0,  level: 1.0);

	sig
}).play;
)

Ndef(\pseudo60).set(\modFreq, 0.5099)
::
